
<div class="profilo_deputato" >
    <img src="{{deputato_full.img.value}}" alt="{{deputato_full.cognome.value}} {{deputato_full.nome.value}}"> <br />
    {{deputato_full.name.value}} <br />
    {{deputato_full.description.value}} <br />
    Data di nascita: {{deputato_full.dataNascita.value | birthDate}} <br />
    Luogo di nascita: {{deputato_full.luogoNascita .value}} <br />
    Sesso: {{deputato_full.gender.value}} <br />
    Partito: {{deputato_full.gruppo_label.value}} <br />
    <div ng-repeat="comm in deputato_commissione">
        Commissione: {{comm}}
    </div>

    <div ng-show="deputato_full.twitter.value">
        Twitter : <a href="{{deputato_full.twitter.value}}">LINK</a>
    </div>
    <div ng-show="deputato_full.facebook.value">
        Facebook: <a href="{{deputato_full.facebook.value}}">LINK</a>
    </div>
    <div ng-show="deputato_full.youtube.value">
        YouTube: <a href="{{deputato_full.youtube.value}}">LINK</a>
    </div>

</div>

<div class="column1">
    <h2>Atto - {{atto_count}}</h2>
    <div class="scrollable">
        <ul ng-repeat="row in atto_result">
            <li><a ng-href="{{row.atto.value}}">{{row.nomeAtto.value}}</a></li>
        </ul>
    </div>
</div>

<div class="column1">
    <h2>Atti di indirizzo e controllo - {{aic_count}}</h2>
    <div class="scrollable">
        <ul ng-repeat="row in aic_result">
            <li><a ng-href="{{row.aic.value}}">{{row.title.value}}</a></li>
        </ul>
    </div>
</div>

<div class="column1">
    <h2>Interventi - {{intervento_count}}</h2>
    <div class="scrollable">
        <ul ng-repeat="row in intervento_result">
            <li><a ng-href="{{row.intervento.value}}">{{row.interventoRif.value}}</a></li>
        </ul>
    </div>
</div>

<div class="column1">
    <h2>Leggi - {{leggi_result.length}}</h2>
    <div class="scrollable">
        <ul ng-repeat="row in leggi_result">
            <li><a ng-href="{{row.legge.value}}">{{row.nomeAtto.value}}</a></li>
        </ul>
    </div>
</div>
<br style="clear:both;">

<script id="select_atto" type="text/ng-template">
SELECT DISTINCT
    ?atto ?nomeAtto ?primo_firmatario ?altro_firmatario
WHERE {
    ?atto a ocd:atto; dc:title ?nomeAtto
    {
        ?atto ocd:primo_firmatario <{{deputato}}>;
        dc:date ?primo_firmatario
    }
UNION
    {
        ?atto ocd:altro_firmatario <{{deputato}}>;
        dc:date ?altro_firmatario
    }
}
</script>

<script id="select_aic" type="text/ng-template">
SELECT DISTINCT
    ?aic ?title ?primo_firmatario ?altro_firmatario
WHERE {
    ?aic a ocd:aic;
    ocd:rif_leg <{{legislatura_uri}}>;
    dc:title ?title .
    {
        ?aic ocd:primo_firmatario <{{deputato}}>;
        dc:identifier ?primo_firmatario
    }
    UNION
    {
        ?aic ocd:altro_firmatario <{{deputato}}>;
        dc:identifier ?altro_firmatario
    }
}
</script>

<script id="select_intervento" type="text/ng-template">
SELECT DISTINCT
    ?intervento ?interventoRif
WHERE {
    ?intervento a ocd:intervento;
    ocd:rif_deputato <{{deputato}}>;
    dc:title ?interventoRif
}
</script>

<script  id="select_legge" type="text/ng-template">
SELECT DISTINCT *
WHERE {
    ?atto a ocd:atto .
    {
        {
            ?atto ocd:primo_firmatario <{{deputato}}>
        }
        UNION
        {
            ?atto ocd:altro_firmatario <{{deputato}}>
        }
        <{{deputato}}> a ocd:deputato
    }
    ?atto dc:title ?nomeAtto .
    ?atto dc:description ?descrizioneAtto .
    ?legge a ocd:legge; ocd:rif_leg <{{legislatura_uri}}> .
    ?legge ocd:lavoriPreparatori [?lavoro ?atto]
}
</script>

<script  id="select_bio" type="text/ng-template">
SELECT DISTINCT
    ?name ?description ?dataNascita ?luogoNascita ?img ?gender ?gruppo_label ?twitter ?facebook ?youtube
WHERE {
    <{{deputato}}> a ocd:deputato;
        dc:description ?description;
        foaf:depiction ?img;
        ocd:rif_leg <{{legislatura_uri}}>;
        ocd:rif_mandatoCamera ?mandato;
        ocd:aderisce ?aderisce .

    ?persona ocd:rif_mandatoCamera ?mandato;
        a foaf:Person;
        dc:title ?name;
        foaf:gender ?gender

    OPTIONAL{
        ?persona bio:Birth ?nascita .
        ?nascita bio:date ?dataNascita; ocd:rif_luogo ?luogoNascitaUri .
        ?luogoNascitaUri dc:title ?luogoNascita
    }
    OPTIONAL{
        ?persona foaf:account ?accountTw .
        ?accountTw foaf:accountServiceHomepage ?twitter .
        FILTER(REGEX(STR(?twitter),'twitter.com'))
    }
    OPTIONAL{
        ?persona foaf:account ?accountFb .
        ?accountFb foaf:accountServiceHomepage ?facebook .
        FILTER(REGEX(STR(?facebook),'facebook.com'))
    }

    OPTIONAL{
        ?persona foaf:account ?accountYt .
        ?accountYt foaf:accountServiceHomepage ?youtube .
        FILTER(REGEX(STR(?youtube),'youtube.com'))
    }

    ?aderisce ocd:rif_gruppoParlamentare ?gruppo .
    ?gruppo <http://purl.org/dc/terms/alternative> ?gruppo_label .
    MINUS{
        ?aderisce ocd:endDate ?fineAdesione
    }
}
</script>

<script  id="select_commissione" type="text/ng-template">
SELECT DISTINCT
    ?commissione
WHERE {
    <{{deputato}}> a ocd:deputato; ocd:membro ?membro .
    ?membro ocd:rif_organo ?organo .
    ?organo dc:title ?commissione
    MINUS{
        ?membro ocd:endDate ?fineMembership
    }
}
</script>